configfile: "../config/config.yaml"

wildcard_constraints:
    tissue = "[A-Za-z0-9_\-]+",
    chrom = "[0-9]+"

KINDS=["cis","trans"] if config["trans"] else ["cis"]

rule all:
    input: expand("../results/{kind}QT-{chrom}-{tissue}.txt",chrom=config["chromosome"],tissue=config["tissue"],kind=KINDS)

rule add_age_cov:
    input: inpath="../input/{tissue}_covariates.txt",age="../input/age_data.txt"
    output: "../results/{tissue}_covariates2.txt"
    log: "../logs/add_age_cov/{tissue}.log"
    benchmark: "../benchmarks/add_age_cov/{tissue}.tsv"
    shell: "Rscript scripts/cov_prepro.R --inpath {input.inpath} --age {input.age} --outpath {output} > {log} 2>&1" 
    
rule decompress:
    input: "../input/chr{chrom}_vcf2.vcf.recode.vcf.bcf"
    output: "../work/chr{chrom}.vcf"
    log: "../logs/decompress/{chrom}.log"
    benchmark: "../benchmarks/decompress/{chrom}.tsv"

    shell: "bcftools view -t chr{wildcards.chrom}:5030912-5040000 {input} | perl -lane 'print unless /^##/' > {output} 2>{log}"
    #shell: "bcftools view {input} | perl -lane 'print unless /^##/' > {output} 2>{log}"
rule map_eqtls:
    input: 
        vcf="../work/chr{chrom}.vcf",
        exp="../input/gene_tpm_2017-06-05_v8_{tissue}.gct.gz",
        bed="../input/Genes_bed.csv",
        covar="../results/{tissue}_covariates2.txt"

    output:
        directory("../work/eqtl_temp_chr{chrom}-{tissue}/")
    params: 
        trans_flag="--trans " if config["trans"] else ""   
    log: "../logs/map_eqtls/{chrom}-{tissue}.log" 
    benchmark: "../benchmarks/map_eqtls/{chrom}-{tissue}.tsv"
    shell: "Rscript scripts/QTL_map_NoMedFilter.R --exp_path {input.exp} --vcf_path {input.vcf} --genes_bed {input.bed} --covars {input.covar} {params.trans_flag} --out {output} > {log} 2>&1" 

if (config["trans"]):

    rule filter_both_medians:
        input:"../work/eqtl_temp_chr{chrom}-{tissue}/" 
        output:cis="../work/Filtered-cisQT-{chrom}-{tissue}.txt",trans="../work/Filtered-transQT-{chrom}-{tissue}.txt"
        log: "../logs/filter_both_medians/{chrom}-{tissue}.log"
        benchmark: "../benchmarks/filter_both_medians/{chrom}-{tissue}.tsv"
        shell: "python filter_by_medians.py --inpath {input} --transoutpath {output.trans} --outpath {output.cis} > {log} 2>&1"

else:
    rule filter_cis_medians:
        input:"../work/eqtl_temp_chr{chrom}-{tissue}/" 
        output:"../work/Filtered-cisQT-{chrom}-{tissue}.txt"
        log: "../logs/filter_cis_medians/{chrom}-{tissue}.log"
        benchmark: "../benchmarks/filter_cis_medians/{chrom}-{tissue}.tsv"
        shell: "python filter_by_medians.py --inpath {input} --outpath {output} > {log} 2>&1"



rule extractQTs:
    input: "../work/Filtered-{kind}QT-{chrom}-{tissue}.txt"
    
    output: "../results/{kind}QT-{chrom}-{tissue}.txt"
    log: "../logs/extractQTs/{kind}-{chrom}-{tissue}.log"
    benchmark: "../benchmarks/extractQTs/{kind}-{chrom}-{tissue}.tsv"
    shell: "cp {input} {output} > {log} 2>&1"
